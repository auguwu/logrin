# üêª‚Äç‚ùÑÔ∏èüè≥Ô∏è‚Äç‚ößÔ∏è Logrin: Modern, async-aware logging framework for C++20
# Copyright (c) 2026 Noel Towa <cutie@floofy.dev>, et al.
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
{
  mkShell,
  stdenv,
  lib,
  stdenvAdapters,
  llvmPackages_21,
  ## os-specific
  ### - linux
  valgrind,
  ### - darwin
  apple-sdk_15,
  ## tools
  pkg-config,
  python3,
  bazel_8,
  bazel-buildtools,
  meson,
  cmake,
  ninja,
  ## lsp
  starpls,
  nil,
  ## libraries
  abseil-cpp,
  gtest,
  ## used for devshell
  bash,
  ripgrep,
  nodejs_24,
}: let
  inherit
    (import ./lib/common.nix {
      inherit stdenv llvmPackages_21 stdenvAdapters lib;
    })
    llvm
    ;

  darwinPackages = [apple-sdk_15];
  linuxPackages = [valgrind];

  packages =
    [
      # For sanitizers
      llvm.compiler-rt
      llvm.libcxx

      llvm.clang-tools
      llvm.bintools
      llvm.lldb

      bazel-buildtools
      pkg-config
      nodejs_24
      python3

      # build systems (we support Bazel, CMake, and Meson)
      bazel_8
      cmake
      meson
      ninja

      # libraries (so it's easier to develop with)
      abseil-cpp
      gtest

      # LSPs
      starpls
      nil
    ]
    ++ (lib.optionals stdenv.isLinux linuxPackages)
    ++ (lib.optionals stdenv.isDarwin darwinPackages);

  mkShell' = mkShell.override {stdenv = llvm.stdenv;};

  devshell = builtins.readFile ../hack/nix/devshell.sh;
  shellHook =
    builtins.replaceStrings [
      "@bash@"
      "@ripgrep@"
      "@compiler-rt@"
      "@libcxx@"
      "@libcxx.dev@"
    ] [
      "${bash}"
      "${ripgrep}"
      "${llvm.compiler-rt.dev}"
      "${llvm.libcxx}"
      "${llvm.libcxx.dev}"
    ]
    devshell;
in
  mkShell' {
    inherit packages shellHook;

    name = "logrin-dev";
  }
